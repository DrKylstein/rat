<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>RAT</title>
        <style> * { margin: 0px; padding: 0px; overflow: hidden;}</style> 
    <script id="vertexShader" type="x-shader/x-vertex">
        varying vec2 f_uv;
        varying vec3 f_normal;
        varying vec4 f_pos;
        void main() {
            f_normal = (modelMatrix * vec4(normal, 0.0)).xyz;
            f_pos = modelMatrix * vec4(position, 1.0);
            f_uv = uv;
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
        }
    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
        uniform vec3 color;
        uniform vec3 backColor;
        varying vec2 f_uv;
        varying vec3 f_normal;
        varying vec4 f_pos;
                
        #if MAX_POINT_LIGHTS > 0
        uniform vec3 pointLightColor[MAX_POINT_LIGHTS];
        uniform vec3 pointLightPosition[MAX_POINT_LIGHTS];
        uniform float pointLightDistance[MAX_POINT_LIGHTS];
        #endif
                
        #ifdef USE_FOG
            uniform vec3 fogColor;
            uniform float fogNear;
            uniform float fogFar;
            
            float fogFactorLinear(
                const float dist,
                const float start,
                const float end
            ) {
                return 1.0 - clamp((end - dist) / (end - start), 0.0, 1.0);
            }
        #endif

        #extension GL_OES_standard_derivatives : enable
        float edgeFactor(vec2 uv){
            vec2 d = fwidth(uv);
            vec2 a3 = smoothstep(vec2(0.0), d*1.5, uv);
            return clamp(min(a3.x, a3.y),0.0,1.0);
        }
        
        void main() {
            vec2 uv = f_uv;//mod(f_uv, 1.01);
            uv.x = 1.0-abs(uv.x*2.0 - 1.0);
            uv.y = 1.0-abs(uv.y*2.0 - 1.0);
            
            vec3 light = vec3(0.0,0.0,0.0);
            
            #if MAX_POINT_LIGHTS > 0
            for(int i = 0; i < MAX_POINT_LIGHTS; i++) {
                float lDistance = 1.0;
                if ( pointLightDistance[ i ] > 0.0 )
                    lDistance = 1.0 - min( ( distance(pointLightPosition[i], f_pos.xyz) / pointLightDistance[ i ] ), 1.0 );
            
                light += pointLightColor[i] * max(dot(f_normal, pointLightPosition[i] - f_pos.xyz), 0.0) * lDistance;
            }
            #endif
            
            vec3 wiresColor = mix(color, min(backColor+light, vec3(1.0,1.0,1.0)), edgeFactor(uv));
            
            #ifdef USE_FOG
                float depth = gl_FragCoord.z / gl_FragCoord.w;
                gl_FragColor.rgb = mix(wiresColor, fogColor, fogFactorLinear(depth,fogNear,fogFar));
            #else
                gl_FragColor.rgb = wiresColor;
            #endif
        }
    </script>
    <script id="effectShader" type="x-shader/x-fragment">
        uniform vec3 color;
        uniform vec3 backColor;
        uniform float time;
        uniform float noiseLevel;
        
        float rand(vec2 v) {
            return fract(sin(dot(v.xy,vec2(12.9898,78.233))) * 43758.5453);
        }
        
        void main() {
            vec4 noise = vec4(
                rand(vec2(gl_FragCoord.x + time, gl_FragCoord.y + 0.23)),
                rand(vec2(gl_FragCoord.x + time, gl_FragCoord.y + 0.66)),
                rand(vec2(gl_FragCoord.x + time, gl_FragCoord.y + 0.42)),
                noiseLevel*0.75);
            vec4 lines = vec4(0.0, 0.0, 0.0, 0.3);
            gl_FragColor = mix(noise, lines, floor(mod(gl_FragCoord.y/2.0, 2.0)));
        }
    </script>
    </head> 
    <body> 
        <img id="font" src="rat_font.png" style="display: none" />
        <script src="three.min.js"></script> 
        <script src="random.js"></script> 
        <script src="rat_colors.js"></script> 
        <script src="rat_shapes.js"></script> 
        <script src="rat_world.js"></script> 
        <script src="rat_controls.js"></script> 
        <script src="rat_objects.js"></script> 
        <script src="rat_main.js"></script> 
    </body>
</html>